/-
Copyright (c) 2024 BjÃ¸rn Kjos-Hanssen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: BjÃ¸rn Kjos-Hanssen, Patrick Massot, Floris van Doorn, Jireh Loreaux, Eric Wieser
-/
module

public import Mathlib.Topology.Order.OrderClosedExtr
public import Mathlib.Analysis.Calculus.Deriv.MeanValue
public import Mathlib.Order.Interval.Set.Basic
public import Mathlib.LinearAlgebra.AffineSpace.Ordered

/-!
# The First- and Second-Derivative Tests

We prove the first-derivative test from calculus, in the strong form given on [Wikipedia](https://en.wikipedia.org/wiki/Derivative_test#First-derivative_test).

The test is proved over the real numbers â„
using `monotoneOn_of_deriv_nonneg` from `Mathlib/Analysis/Calculus/Deriv/MeanValue.lean`.

We prove the second-derivative test using the first-derivative test.
Source: [Wikipedia](https://en.wikipedia.org/wiki/Derivative_test#Proof_of_the_second-derivative_test).

## Main results

* `isLocalMax_of_deriv_Ioo`: Suppose `f` is a real-valued function of a real variable
  defined on some interval containing the point `a`.
  Further suppose that `f` is continuous at `a` and differentiable on some open interval
  containing `a`, except possibly at `a` itself.

  If there exists a positive number `r > 0` such that for every `x` in `(a âˆ’ r, a)`
  we have `fâ€²(x) â‰¥ 0`, and for every `x` in `(a, a + r)` we have `fâ€²(x) â‰¤ 0`,
  then `f` has a local maximum at `a`.

* `isLocalMin_of_deriv_Ioo`: The dual of `first_derivative_max`, for minima.

* `isLocalMax_of_deriv`: 1st derivative test for maxima using filters.

* `isLocalMin_of_deriv`: 1st derivative test for minima using filters.

* `isLocalMin_of_deriv_deriv_pos`: The second-derivative test, minimum version.


## Tags

derivative test, first-derivative test, second-derivative test, calculus
-/

public section


open Set Topology

/-- If `f` is continuous at `b` and differentiable on `(a, b)`, then `f` is continuous on
`(a, b]`. -/
private lemma continuousOn_Ioc {f : â„ â†’ â„} {a b : â„} (gâ‚€ : a < b) (h : ContinuousAt f b)
    (hdâ‚€ : DifferentiableOn â„ f (Ioo a b)) : ContinuousOn f (Ioc a b) :=
  Ioo_union_right gâ‚€ â–¸ hdâ‚€.continuousOn.union_continuousAt isOpen_Ioo (by simp_all)

/-- If `f` is continuous at `a` and differentiable on `(a, b)`, then `f` is continuous on
`[a, b)`. -/
private lemma continuousOn_Ico {f : â„ â†’ â„} {a b : â„} (gâ‚€ : a < b) (h : ContinuousAt f a)
    (hdâ‚€ : DifferentiableOn â„ f (Ioo a b)) : ContinuousOn f (Ico a b) :=
  Ioo_union_left gâ‚€ â–¸ hdâ‚€.continuousOn.union_continuousAt isOpen_Ioo (by simp_all)

/-- Suppose `a < b < c`, `f : â„ â†’ â„` is continuous at `b`, the derivative `f'` is nonnegative on
`(a, b)`, and the derivative `f'` is nonpositive on `(b, c)`. Then `f` attains its maximum on
`(a, c)` at `b`. -/
lemma isMaxOn_of_deriv_Ioo {f : â„ â†’ â„} {a b c : â„} (gâ‚€ : a < b) (gâ‚ : b < c)
    (h : ContinuousAt f b)
    (hdâ‚€ : DifferentiableOn â„ f (Ioo a b))
    (hdâ‚ : DifferentiableOn â„ f (Ioo b c))
    (hâ‚€ : âˆ€ x âˆˆ Ioo a b, 0 â‰¤ deriv f x)
    (hâ‚ : âˆ€ x âˆˆ Ioo b c, deriv f x â‰¤ 0) : IsMaxOn f (Ioo a c) b :=
  isMaxOn_of_mono_anti_Ioo gâ‚€ gâ‚
    (monotoneOn_of_deriv_nonneg (convex_Ioc a b) (continuousOn_Ioc gâ‚€ h hdâ‚€) (by simp_all)
    (by simp_all))
    (antitoneOn_of_deriv_nonpos (convex_Ico b c) (continuousOn_Ico gâ‚ h hdâ‚) (by simp_all)
    (by simp_all))

/-- Suppose `a < b â‰¤ c`, `f : â„ â†’ â„` is continuous at `b` and `c`, the derivative `f'` is
nonnegative on `(a, b)`, and the derivative `f'` is nonpositive on `(b, c)`. Then `f` attains its
maximum on `(a, c]` at `b`. -/
lemma isMaxOn_of_deriv_Ioc {f : â„ â†’ â„} {a b c : â„} (gâ‚€ : a < b) (gâ‚ : b â‰¤ c)
    (hb : ContinuousAt f b) (hc : ContinuousAt f c)
    (hdâ‚€ : DifferentiableOn â„ f (Ioo a b))
    (hdâ‚ : DifferentiableOn â„ f (Ioo b c))
    (hâ‚€ : âˆ€ x âˆˆ Ioo a b, 0 â‰¤ deriv f x)
    (hâ‚ : âˆ€ x âˆˆ Ioo b c, deriv f x â‰¤ 0) : IsMaxOn f (Ioc a c) b :=
  have hIoc : ContinuousOn f (Ioc a b) := Ioo_union_right gâ‚€ â–¸
    hdâ‚€.continuousOn.union_continuousAt isOpen_Ioo (by simp_all)
  have hIcc : ContinuousOn f (Icc b c) := Ioo_union_both gâ‚ â–¸
    hdâ‚.continuousOn.union_continuousAt isOpen_Ioo (by simp_all)
  isMaxOn_of_mono_anti_Ioc gâ‚€ gâ‚
    (monotoneOn_of_deriv_nonneg (convex_Ioc a b) hIoc (by simp_all) (by simp_all))
    (antitoneOn_of_deriv_nonpos (convex_Icc b c) hIcc (by simp_all) (by simp_all))

/-- Suppose `a â‰¤ b < c`, `f : â„ â†’ â„` is continuous at `b` and `c`, the derivative `f'` is
nonnegative on `(a,b)`, and the derivative `f'` is nonpositive on `(b,c)`. Then `f` attains its
maximum on `(a,c]` at `b`. -/
lemma isMaxOn_of_deriv_Ico {f : â„ â†’ â„} {a b c : â„} (gâ‚€ : a â‰¤ b) (gâ‚ : b < c)
    (ha : ContinuousAt f a) (hb : ContinuousAt f b)
    (hdâ‚€ : DifferentiableOn â„ f (Ioo a b))
    (hdâ‚ : DifferentiableOn â„ f (Ioo b c))
    (hâ‚€ : âˆ€ x âˆˆ Ioo a b, 0 â‰¤ deriv f x)
    (hâ‚ : âˆ€ x âˆˆ Ioo b c, deriv f x â‰¤ 0) : IsMaxOn f (Ico a c) b :=
  have hIcc : ContinuousOn f (Icc a b) := Ioo_union_both gâ‚€ â–¸
    hdâ‚€.continuousOn.union_continuousAt isOpen_Ioo (by simp_all)
  have hIco : ContinuousOn f (Ico b c) := Ioo_union_left gâ‚ â–¸
    hdâ‚.continuousOn.union_continuousAt isOpen_Ioo (by simp_all)
  isMaxOn_of_mono_anti_Ico gâ‚€ gâ‚
    (monotoneOn_of_deriv_nonneg (convex_Icc a b) hIcc (by simp_all) (by simp_all))
    (antitoneOn_of_deriv_nonpos (convex_Ico b c) hIco (by simp_all) (by simp_all))

/-- The First-Derivative Test from calculus, maxima version.
Suppose `a < b < c`, `f : â„ â†’ â„` is continuous at `b`,
the derivative `f'` is nonnegative on `(a,b)`, and
the derivative `f'` is nonpositive on `(b,c)`. Then `f` has a local maximum at `b`. -/
lemma isLocalMax_of_deriv_Ioo {f : â„ â†’ â„} {a b c : â„} (gâ‚€ : a < b) (gâ‚ : b < c)
    (h : ContinuousAt f b)
    (hdâ‚€ : DifferentiableOn â„ f (Ioo a b))
    (hdâ‚ : DifferentiableOn â„ f (Ioo b c))
    (hâ‚€ : âˆ€ x âˆˆ Ioo a b, 0 â‰¤ deriv f x)
    (hâ‚ : âˆ€ x âˆˆ Ioo b c, deriv f x â‰¤ 0) : IsLocalMax f b :=
  (isMaxOn_of_deriv_Ioo gâ‚€ gâ‚ h hdâ‚€ hdâ‚ hâ‚€ hâ‚).isLocalMax (Ioo_mem_nhds gâ‚€ gâ‚)

/-- Suppose `a < b < c`, `f : â„ â†’ â„` is continuous at `b`, the derivative `f'` is nonpositive on
`(a,b)`, and the derivative `f'` is nonnegative on `(b,c)`. Then `f` attains its minimum on `(a,c)`
at `b`. -/
lemma isMinOn_of_deriv_Ioo {f : â„ â†’ â„} {a b c : â„} (gâ‚€ : a < b) (gâ‚ : b < c)
    (h : ContinuousAt f b)
    (hdâ‚€ : DifferentiableOn â„ f (Ioo a b))
    (hdâ‚ : DifferentiableOn â„ f (Ioo b c))
    (hâ‚€ : âˆ€ x âˆˆ Ioo a b, deriv f x â‰¤ 0)
    (hâ‚ : âˆ€ x âˆˆ Ioo b c, 0 â‰¤ deriv f x) : IsMinOn f (Ioo a c) b :=
  have hIoc : ContinuousOn f (Ioc a b) := Ioo_union_right gâ‚€ â–¸
    hdâ‚€.continuousOn.union_continuousAt isOpen_Ioo (by simp_all)
  have hIco : ContinuousOn f (Ico b c) := Ioo_union_left gâ‚ â–¸
    hdâ‚.continuousOn.union_continuousAt isOpen_Ioo (by simp_all)
  isMinOn_of_anti_mono_Ioo gâ‚€ gâ‚
    (antitoneOn_of_deriv_nonpos (convex_Ioc a b) hIoc (by simp_all) (by simp_all))
    (monotoneOn_of_deriv_nonneg (convex_Ico b c) hIco (by simp_all) (by simp_all))

/-- The First-Derivative Test from calculus, minima version. -/
lemma isLocalMin_of_deriv_Ioo {f : â„ â†’ â„} {a b c : â„}
    (gâ‚€ : a < b) (gâ‚ : b < c) (h : ContinuousAt f b)
    (hdâ‚€ : DifferentiableOn â„ f (Ioo a b)) (hdâ‚ : DifferentiableOn â„ f (Ioo b c))
    (hâ‚€ : âˆ€ x âˆˆ Ioo a b, deriv f x â‰¤ 0)
    (hâ‚ : âˆ€ x âˆˆ Ioo b c, 0 â‰¤ deriv f x) : IsLocalMin f b :=
  (isMinOn_of_deriv_Ioo gâ‚€ gâ‚ h hdâ‚€ hdâ‚ hâ‚€ hâ‚).isLocalMin (Ioo_mem_nhds gâ‚€ gâ‚)

/-- The First-Derivative Test from calculus, maxima version,
expressed in terms of left and right filters. -/
lemma isLocalMax_of_deriv' {f : â„ â†’ â„} {b : â„} (h : ContinuousAt f b)
    (hdâ‚€ : âˆ€á¶  x in ğ“[<] b, DifferentiableAt â„ f x) (hdâ‚ : âˆ€á¶  x in ğ“[>] b, DifferentiableAt â„ f x)
    (hâ‚€ : âˆ€á¶  x in ğ“[<] b, 0 â‰¤ deriv f x) (hâ‚ : âˆ€á¶  x in ğ“[>] b, deriv f x â‰¤ 0) :
    IsLocalMax f b := by
  obtain âŸ¨a, haâŸ© := (nhdsLT_basis b).eventually_iff.mp <| hdâ‚€.and hâ‚€
  obtain âŸ¨c, hcâŸ© := (nhdsGT_basis b).eventually_iff.mp <| hdâ‚.and hâ‚
  exact isLocalMax_of_deriv_Ioo ha.1 hc.1 h
    (fun _ hx => (ha.2 hx).1.differentiableWithinAt)
    (fun _ hx => (hc.2 hx).1.differentiableWithinAt)
    (fun _ hx => (ha.2 hx).2) (fun x hx => (hc.2 hx).2)

/-- The First-Derivative Test from calculus, minima version,
expressed in terms of left and right filters. -/
lemma isLocalMin_of_deriv' {f : â„ â†’ â„} {b : â„} (h : ContinuousAt f b)
    (hdâ‚€ : âˆ€á¶  x in ğ“[<] b, DifferentiableAt â„ f x) (hdâ‚ : âˆ€á¶  x in ğ“[>] b, DifferentiableAt â„ f x)
    (hâ‚€ : âˆ€á¶  x in ğ“[<] b, deriv f x â‰¤ 0) (hâ‚ : âˆ€á¶  x in ğ“[>] b, deriv f x â‰¥ 0) :
    IsLocalMin f b := by
  obtain âŸ¨a, haâŸ© := (nhdsLT_basis b).eventually_iff.mp <| hdâ‚€.and hâ‚€
  obtain âŸ¨c, hcâŸ© := (nhdsGT_basis b).eventually_iff.mp <| hdâ‚.and hâ‚
  exact isLocalMin_of_deriv_Ioo ha.1 hc.1 h
    (fun _ hx => (ha.2 hx).1.differentiableWithinAt)
    (fun _ hx => (hc.2 hx).1.differentiableWithinAt)
    (fun _ hx => (ha.2 hx).2) (fun x hx => (hc.2 hx).2)

/-- The First Derivative test, maximum version. -/
theorem isLocalMax_of_deriv {f : â„ â†’ â„} {b : â„} (h : ContinuousAt f b)
    (hd : âˆ€á¶  x in ğ“[â‰ ] b, DifferentiableAt â„ f x)
    (hâ‚€ : âˆ€á¶  x in ğ“[<] b, 0 â‰¤ deriv f x) (hâ‚ : âˆ€á¶  x in ğ“[>] b, deriv f x â‰¤ 0) :
    IsLocalMax f b :=
  isLocalMax_of_deriv' h (nhdsLT_le_nhdsNE _ (by tauto)) (nhdsGT_le_nhdsNE _ (by tauto)) hâ‚€ hâ‚

/-- The First Derivative test, minimum version. -/
theorem isLocalMin_of_deriv {f : â„ â†’ â„} {b : â„} (h : ContinuousAt f b)
    (hd : âˆ€á¶  x in ğ“[â‰ ] b, DifferentiableAt â„ f x)
    (hâ‚€ : âˆ€á¶  x in ğ“[<] b, deriv f x â‰¤ 0) (hâ‚ : âˆ€á¶  x in ğ“[>] b, 0 â‰¤ deriv f x) :
    IsLocalMin f b :=
  isLocalMin_of_deriv' h (nhdsLT_le_nhdsNE _ (by tauto)) (nhdsGT_le_nhdsNE _ (by tauto)) hâ‚€ hâ‚

open Filter SignType

section SecondDeriv

variable {f : â„ â†’ â„} {xâ‚€ : â„}

/-- If the derivative of `f` is positive at a root `xâ‚€` of `f`, then locally the sign of `f x`
matches `x - xâ‚€`. -/
lemma eventually_nhdsWithin_sign_eq_of_deriv_pos (hf : deriv f xâ‚€ > 0) (hx : f xâ‚€ = 0) :
    âˆ€á¶  x in ğ“ xâ‚€, sign (f x) = sign (x - xâ‚€) := by
  rw [â† nhdsNE_sup_pure xâ‚€, eventually_sup]
  refine âŸ¨?_, by simpaâŸ©
  have h_tendsto := hasDerivAt_iff_tendsto_slope.mp
    (differentiableAt_of_deriv_ne_zero <| ne_of_gt hf).hasDerivAt
  filter_upwards [(h_tendsto.eventually <| eventually_gt_nhds hf),
    self_mem_nhdsWithin] with x hxâ‚€ hxâ‚
  rw [mem_compl_iff, mem_singleton_iff, â† Ne.eq_def] at hxâ‚
  obtain (hx' | hx') := hxâ‚.lt_or_gt
  Â· rw [sign_neg (neg_of_slope_pos hx' hxâ‚€ hx), sign_neg (sub_neg.mpr hx')]
  Â· rw [sign_pos (pos_of_slope_pos hx' hxâ‚€ hx), sign_pos (sub_pos.mpr hx')]

/-- If the derivative of `f` is negative at a root `xâ‚€` of `f`, then locally the sign of `f x`
matches `xâ‚€ - x`. -/
lemma eventually_nhdsWithin_sign_eq_of_deriv_neg (hf : deriv f xâ‚€ < 0) (hx : f xâ‚€ = 0) :
    âˆ€á¶  x in ğ“ xâ‚€, sign (f x) = sign (xâ‚€ - x) := by
  simpa [Left.sign_neg, -neg_sub, â† neg_sub xâ‚€] using
    eventually_nhdsWithin_sign_eq_of_deriv_pos
      (f := (-f Â·)) (xâ‚€ := xâ‚€) (by simpa [deriv.neg]) (by simpa)

lemma deriv_neg_left_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„}
    (hâ‚€ : âˆ€á¶  (x : â„) in ğ“[â‰ ] xâ‚€, sign (deriv f x) = sign (x - xâ‚€)) :
    âˆ€á¶  (b : â„) in ğ“[<] xâ‚€, deriv f b < 0 := by
  filter_upwards [nhdsLT_le_nhdsNE _ hâ‚€, self_mem_nhdsWithin] with x hx' (hx : x < xâ‚€)
  rwa [â† sub_neg, â† sign_eq_neg_one_iff, â† hx', sign_eq_neg_one_iff] at hx

lemma deriv_neg_right_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„}
    (hâ‚€ : âˆ€á¶  (x : â„) in ğ“[â‰ ] xâ‚€, sign (deriv f x) = sign (xâ‚€ - x)) :
     âˆ€á¶  (b : â„) in ğ“[>] xâ‚€, deriv f b < 0 := by
  filter_upwards [nhdsGT_le_nhdsNE _ hâ‚€, self_mem_nhdsWithin] with x hx' (hx : xâ‚€ < x)
  rwa [â† sub_neg, â† sign_eq_neg_one_iff, â† hx', sign_eq_neg_one_iff] at hx

lemma deriv_pos_right_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„}
    (hâ‚€ : âˆ€á¶  (x : â„) in ğ“[â‰ ] xâ‚€, sign (deriv f x) = sign (x - xâ‚€)) :
     âˆ€á¶  (b : â„) in ğ“[>] xâ‚€, deriv f b > 0 := by
  filter_upwards [nhdsGT_le_nhdsNE _ hâ‚€, self_mem_nhdsWithin] with x hx' (hx : xâ‚€ < x)
  rwa [â† sub_pos, â† sign_eq_one_iff, â† hx', sign_eq_one_iff] at hx

lemma deriv_pos_left_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„}
    (hâ‚€ : âˆ€á¶  (x : â„) in ğ“[â‰ ] xâ‚€, sign (deriv f x) = sign (xâ‚€ - x)) :
    âˆ€á¶  (b : â„) in ğ“[<] xâ‚€, deriv f b > 0 := by
  filter_upwards [nhdsLT_le_nhdsNE _ hâ‚€, self_mem_nhdsWithin] with x hx' (hx : x < xâ‚€)
  rwa [â† sub_pos, â† sign_eq_one_iff, â† hx', sign_eq_one_iff] at hx

/-- The First Derivative test with a hypothesis on the sign of the derivative, maximum version. -/
theorem isLocalMax_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„} (h : ContinuousAt f xâ‚€)
    (hf : âˆ€á¶  x in ğ“[â‰ ] xâ‚€, sign (deriv f x) = sign (xâ‚€ - x)) :
    IsLocalMax f xâ‚€ := by
  have hl := deriv_pos_left_of_sign_deriv hf
  have hg := deriv_neg_right_of_sign_deriv hf
  replace hf := (nhdsLT_sup_nhdsGT xâ‚€) â–¸
    eventually_sup.mpr âŸ¨hl.mono fun x hx => hx.ne', hg.mono fun x hx => hx.neâŸ©
  exact isLocalMax_of_deriv h (hf.mono fun x hx â†¦ differentiableAt_of_deriv_ne_zero hx)
    (hl.mono fun _ => le_of_lt) (hg.mono fun _ => le_of_lt)

/-- The First Derivative test with a hypothesis on the sign of the derivative, minimum version. -/
theorem isLocalMin_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„} (h : ContinuousAt f xâ‚€)
    (hf : âˆ€á¶  x in ğ“[â‰ ] xâ‚€, sign (deriv f x) = sign (x - xâ‚€)) :
    IsLocalMin f xâ‚€ := by
  refine neg_neg f â–¸ (isLocalMax_of_sign_deriv (f := (-f Â·)) h.neg ?foo |>.neg)
  simpa [Left.sign_neg, -neg_sub, â† neg_sub _ xâ‚€, deriv.neg]

/-- The Second-Derivative Test from calculus, minimum version.
Applies to functions like `x^2 + 1[x â‰¥ 0]` as well as twice differentiable
functions. -/
theorem isLocalMin_of_deriv_deriv_pos (hf : deriv (deriv f) xâ‚€ > 0) (hd : deriv f xâ‚€ = 0)
    (hc : ContinuousAt f xâ‚€) : IsLocalMin f xâ‚€ :=
  isLocalMin_of_sign_deriv hc <| nhdsWithin_le_nhds <|
    eventually_nhdsWithin_sign_eq_of_deriv_pos hf hd

/-- The Second-Derivative Test from calculus, maximum version. -/
theorem isLocalMax_of_deriv_deriv_neg (hf : deriv (deriv f) xâ‚€ < 0) (hd : deriv f xâ‚€ = 0)
    (hc : ContinuousAt f xâ‚€) : IsLocalMax f xâ‚€ := by
  simpa using isLocalMin_of_deriv_deriv_pos (by simpa) (by simpa) hc.neg |>.neg

end SecondDeriv
