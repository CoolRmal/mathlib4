/-
Copyright (c) 2026 Yongxi Lin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yongxi Lin, Thomas Zhu
-/
module

public import Mathlib.MeasureTheory.Function.ConditionalExpectation.Indicator

import Mathlib.Analysis.Convex.Approximation
import Mathlib.Analysis.Convex.Continuous

/-!
# Conditional Jensen's Inequality

This file contains the conditional Jensen's inequality. We follow the proof in
[Hytonen_VanNeerven_Veraar_Wies_2016].

## Main Statement

* `conditional_jensen`: in a Banach space `E` with finite measure `Î¼`, if `Ï† : E â†’ â„` is a convex
  lower-semicontinuous function, then for any `f : Î± â†’ E` such that `f` and `Ï† âˆ˜ f` are integrable,
  we have `Ï† (ğ”¼[f | m]) â‰¤ ğ”¼[Ï† âˆ˜ f | m]`.

-/

@[expose] public section

open MeasureTheory Function Set

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  {Î± : Type*} {f : Î± â†’ E} {Ï† : E â†’ â„} {m mÎ± : MeasurableSpace Î±} {Î¼ : Measure Î±}

/-- A measure is called semifinite if any measurable set with positive measure has a subset with
finite positive measure. -/
class SemiFinite (Î¼ : Measure Î±) : Prop where
  exists_lt_top â¦ƒs : Set Î±â¦„ (hms : MeasurableSet s) (hs : 0 < Î¼ s) : âˆƒ t, t âŠ† s âˆ§ 0 < Î¼ t âˆ§ Î¼ t < âŠ¤

/-- A measure is semifinite iff any measurable set with positive measure has a measurable subset
with finite positive measure. -/
theorem SemiFinite.iff :
    SemiFinite Î¼ â†” âˆ€ s, MeasurableSet s â†’ 0 < Î¼ s â†’
      âˆƒ t, MeasurableSet t âˆ§ t âŠ† s âˆ§ 0 < Î¼ t âˆ§ Î¼ t < âŠ¤ where
  mp h s hms hs := by
    obtain âŸ¨t, htâŸ© := h.exists_lt_top hms hs
    refine âŸ¨s âˆ© toMeasurable Î¼ t, hms.inter ?_, inter_subset_left, ?_, ?_âŸ©
    Â· exact measurableSet_toMeasurable Î¼ t
    Â· exact ht.2.1.trans_le (measure_mono (subset_inter ht.1 (subset_toMeasurable Î¼ t)))
    Â· exact ((measure_toMeasurable t) â–¸ ht.2.2).trans_le' (measure_mono inter_subset_right)
  mpr h := by
    constructor
    intro s hms hs
    obtain âŸ¨t, htâŸ© := h s hms hs
    exact âŸ¨t, ht.2âŸ©

/-- A measure is semifinite iff any null measurable set with positive measure has a subset with
finite positive measure. -/
theorem Semifinite.iff_nullMeasurable :
    SemiFinite Î¼ â†” âˆ€ s, NullMeasurableSet s Î¼ â†’ 0 < Î¼ s â†’ âˆƒ t, t âŠ† s âˆ§ 0 < Î¼ t âˆ§ Î¼ t < âŠ¤ where
  mp h s hms hs := by
    obtain âŸ¨t, htâŸ© := h.exists_lt_top (measurableSet_toMeasurable Î¼ s) (by simp [hs])
    have : Î¼ (t âˆ© s) = Î¼ t :=
      measure_inter_conull' (Î¼.mono_null (by grind) (ae_eq_set.1 hms.toMeasurable_ae_eq).1)
    exact âŸ¨t âˆ© s, inter_subset_right, this â–¸ ht.2.1, this â–¸ ht.2.2âŸ©
  mpr h := by
    constructor
    exact fun s hs hs' => h s hs.nullMeasurableSet hs'

theorem measure_eq_zero_of_measure_inter_finite_eq_zero [SemiFinite Î¼]
    {s : Set Î±} (hs : âˆ€ t, Î¼ t < âŠ¤ â†’ Î¼ (s âˆ© t) = 0) : Î¼ s = 0 := by
  by_contra! hne
  obtain âŸ¨t, htâŸ© := SemiFinite.exists_lt_top (by positivity)
  have := Set.inter_eq_self_of_subset_right ht.1 â–¸ hs t ht.2.2
  grind

instance [SigmaFinite Î¼] : SemiFinite Î¼ where
  exists_lt_top s hs := by
    obtain âŸ¨n, hnâŸ© := (Î¼.exists_measure_inter_spanningSets_pos s).2 hs
    refine âŸ¨s âˆ© spanningSets Î¼ n, Set.inter_subset_left, hn, ?_âŸ©
    exact (measure_spanningSets_lt_top Î¼ n).trans_le' (measure_mono Set.inter_subset_right)

theorem ae_iff_ae_restrict [SemiFinite Î¼] {p : Î± â†’ Prop}
    (hp : âˆ€ t, MeasurableSet t â†’ Î¼ t < âŠ¤ â†’ âˆ€áµ a âˆ‚(Î¼.restrict t), p a) :
    âˆ€áµ a âˆ‚Î¼, p a := by
  simp_all only [ae_iff]
  refine measure_eq_zero_of_measure_inter_finite_eq_zero fun t ht => ?_
  simpa [â† Î¼.restrict_apply' ht] using hp t ht ht'

/-- Conditional Jensen's inequality for hereditarily Lindelof Spaces. -/
private lemma conditional_jensen_of_hereditarilyLindelofSpace [IsFiniteMeasure Î¼]
    [HereditarilyLindelofSpace E] (hm : m â‰¤ mÎ±) (hÏ†_cvx : ConvexOn â„ Set.univ Ï†)
    (hÏ†_cont : LowerSemicontinuous Ï†) (hf_int : Integrable f Î¼) (hÏ†_int : Integrable (Ï† âˆ˜ f) Î¼) :
    âˆ€áµ a âˆ‚Î¼, Ï† (Î¼[f | m] a) â‰¤ Î¼[Ï† âˆ˜ f | m] a := by
  obtain âŸ¨L, c, hLcâŸ© := hÏ†_cvx.real_univ_sSup_of_nat_affine_eq hÏ†_cont
  have hp := ae_all_iff.2 fun i => (L i).comp_condExp_add_const_comm hm hf_int (c i)
  have hw : âˆ€áµ a âˆ‚Î¼, âˆ€ i : â„•, Î¼[(L i) âˆ˜ f + const Î± (c i) | m] a â‰¤ Î¼[Ï† âˆ˜ f | m] a := by
    refine ae_all_iff.2 fun i => condExp_mono ?_ hÏ†_int ?_
    Â· exact ((L i).integrable_comp hf_int).add (integrable_const (c i))
    Â· filter_upwards with a
      simp only [Pi.add_apply, comp_apply, const_apply, â† congrFun hLc.2 (f a), iSup_apply]
      exact le_ciSup (Î± := â„) (bddAbove_def.2 âŸ¨Ï† (f a), fun r âŸ¨z, hzâŸ© => hz â–¸ hLc.1 z (f a)âŸ©) i
  filter_upwards [hp, hw] with a hp hw
  simpa [â† hLc.2, iSup_congr hp] using ciSup_le hw

/-- Conditional Jensen's inequality for finite measures. -/
private theorem conditional_jensen_of_finite [IsFiniteMeasure Î¼] (hm : m â‰¤ mÎ±)
    (hÏ†_cvx : ConvexOn â„ Set.univ Ï†) (hÏ†_cont : LowerSemicontinuous Ï†)
    (hf_int : Integrable f Î¼) (hÏ†_int : Integrable (Ï† âˆ˜ f) Î¼) :
    Ï† âˆ˜ Î¼[f | m] â‰¤áµ[Î¼] Î¼[Ï† âˆ˜ f | m] := by
  borelize E
  obtain âŸ¨t, ht, httâŸ© := hf_int.aestronglyMeasurable.isSeparable_ae_range
  let Y := (Submodule.span â„ t).topologicalClosure
  have : CompleteSpace Y := (Submodule.isClosed_topologicalClosure _).completeSpace_coe
  have : SecondCountableTopology Y := ht.span.closure.secondCountableTopology
  let Ï†Y := Ï† âˆ˜ Y.subtypeL
  have aeinY : âˆ€áµ (x : Î±) âˆ‚Î¼, f x âˆˆ Y := by filter_upwards [htt] with a ha using
    (subset_trans Submodule.subset_span subset_closure) ha
  classical
  let fY : Î± â†’ Y := fun a => if h : f a âˆˆ Y then âŸ¨f a, hâŸ© else 0
  let fX : Î± â†’ E := Y.subtypeL âˆ˜ fY
  have lem1 : f =áµ[Î¼] fX := by filter_upwards [aeinY] with a ha; simp [fX, fY, ha, reduceDIte]
  have hfX_int : Integrable fX Î¼ := Integrable.congr hf_int lem1
  have hfY_int : Integrable fY Î¼ := by
    refine âŸ¨?_, hfX_int.2.mono (by simp [fX])âŸ©
    have hs : MeasurableSet (Y : Set E) := (Submodule.isClosed_topologicalClosure _).measurableSet
    have h_nonempty : (Y : Set E).Nonempty := Set.Nonempty.of_subtype
    obtain âŸ¨g, hg1, hg2, hg3âŸ© := hf_int.1.exists_stronglyMeasurable_range_subset hs h_nonempty aeinY
    refine âŸ¨Set.codRestrict g Y hg2, (hg1.measurable.codRestrict hg2).stronglyMeasurable, ?_âŸ©
    filter_upwards [hg3] with a ha1
    have : g a âˆˆ Y := hg2 a
    simp_all [fY, Set.codRestrict]
  have lem2 : Î¼[f | m] =áµ[Î¼] Y.subtypeL âˆ˜ Î¼[fY | m] := calc
    _ =áµ[Î¼] Î¼[fX | m] := condExp_congr_ae lem1
    _ =áµ[Î¼] _ := (Y.subtypeL.comp_condExp_comm hfY_int).symm
  have lem3 : Ï† âˆ˜ f =áµ[Î¼] Ï†Y âˆ˜ fY := by filter_upwards [lem1] with a ha; simp [Ï†Y, ha, fX]
  calc
    Ï† âˆ˜ Î¼[f | m]
      =áµ[Î¼] Ï†Y âˆ˜ Î¼[fY | m] := by filter_upwards [lem2] with a ha; simp [Ï†Y, ha]
    _ â‰¤áµ[Î¼] Î¼[Ï†Y âˆ˜ fY | m] := conditional_jensen_of_hereditarilyLindelofSpace hm
      (hÏ†_cvx.comp_linearMap Y.subtype) (hÏ†_cont.comp Y.subtypeL.cont) hfY_int (hÏ†_int.congr lem3)
    _ =áµ[Î¼] Î¼[Ï† âˆ˜ f | m] := condExp_congr_ae lem3.symm

theorem condExp_restrict_ae_eq_restrict {s : Set Î±} (hm : m â‰¤ mÎ±) [SemiFinite (Î¼.trim hm)]
    (hs_m : MeasurableSet[m] s) (hf_int : Integrable f Î¼) :
    (Î¼.restrict s)[f | m] =áµ[Î¼.restrict s] Î¼[f | m] := by
  sorry

/-- **Conditional Jensen's inequality**: in a Banach space `X` with a semifinite measure `Î¼`, if
`Ï† : X â†’ â„` is a convex lower-semicontinuous function, then for any `f : Î± â†’ X` such that `f` and
`Ï† âˆ˜ f` are integrable, we have `Ï† (ğ”¼[f | m]) â‰¤ ğ”¼[Ï† âˆ˜ f | m]`. -/
theorem conditional_jensen (hm : m â‰¤ mÎ±) [SemiFinite Î¼]
    (hÏ†_cvx : ConvexOn â„ Set.univ Ï†) (hÏ†_cont : LowerSemicontinuous Ï†)
    (hf_int : Integrable f Î¼) (hÏ†_int : Integrable (Ï† âˆ˜ f) Î¼) :
    Ï† âˆ˜ Î¼[f | m] â‰¤áµ[Î¼] Î¼[Ï† âˆ˜ f | m] := by
  refine ae_iff_ae_restrict ?_ fun t ht ht' => ?_
  Â· sorry
  Â· have := Restrict.isFiniteMeasure Î¼ (hs := fact_iff.2 ht')
    have := conditional_jensen_of_finite (Î¼ := Î¼.restrict t) hm hÏ†_cvx hÏ†_cont hf_int.restrict hÏ†_int.restrict

variable [FiniteDimensional â„ E]

/-- **Conditional Jensen's inequality**: in a finite dimesnional Banach space `X` with a finite
measure `Î¼`, if `Ï† : X â†’ â„` is a convex function, then for any `f : Î± â†’ X` such that `f` and
`Ï† âˆ˜ f` are integrable, we have `Ï† (ğ”¼[f | m]) â‰¤ ğ”¼[Ï† âˆ˜ f | m]`. -/
theorem conditional_jensen_finite_dim [SemiFinite Î¼] (hm : m â‰¤ mÎ±)
    (hÏ†_cvx : ConvexOn â„ Set.univ Ï†) (hf_int : Integrable f Î¼) (hÏ†_int : Integrable (Ï† âˆ˜ f) Î¼) :
    Ï† âˆ˜ Î¼[f | m] â‰¤áµ[Î¼] Î¼[Ï† âˆ˜ f | m] :=
  conditional_jensen hm hÏ†_cvx
    (continuousOn_univ.1 (hÏ†_cvx.continuousOn isOpen_univ)).lowerSemicontinuous hf_int hÏ†_int
